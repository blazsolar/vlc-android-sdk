buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.7.0'
    }
}

import org.ajoberstar.grgit.Grgit

class Architecture {

    String taskName
    String architecture
    String mode

    Architecture(taskName, architecture, mode) {
        this.taskName = taskName
        this.architecture = architecture
        this.mode = mode
    }

}

def architectures = [
        new Architecture('Arm', 'armeabi-v7a', '--release'),
        new Architecture('Arm64', 'arm64-v8a', '--release'),
//        new Architecture('Mips', 'mips', '--release'),
        new Architecture('Mips64', 'mips64', '--release'),
        new Architecture('X86', 'x86', '--release'),
        new Architecture('X86_64', 'x86_64', '--release')
]

tasks.withType(Javadoc).all { enabled = false }

task vlcPurge(type: Delete, dependsOn: ['vlcPurgeJava', 'vlcPurgeJni']) {
    group = 'Vlc'
    description = 'Delete VLC Java and jnilibs directories'
}

task vlcPurgeJava(type: Delete) {
    group = 'Vlc'
    description = 'Delete VLC Java directories'
    delete 'src/main/java'
}

task vlcPurgeJni(type: Delete) {
    group = 'Vlc'
    description = 'Delete VLC jni directories'
    delete 'src/main/jniLibs'
}

architectures.each {
    def a = it
    def compileArchitectureTaskName = "vlcCompile${a.taskName}"
    task "${compileArchitectureTaskName}"(type: Exec) {
        group = 'Vlc'
        description = "Compile ${a.taskName} VLC architecture"
        workingDir project.ext.vlcAndroidSource
        commandLine './compile-libvlc.sh'
        args '-a', a.architecture
        args a.mode
        onlyIf { project.ext.vlcAndroidSource.exists() }
    }

    task "vlcBuild${a.taskName}"(type: Copy, dependsOn: compileArchitectureTaskName) {
        from "${project.ext.vlcAndroidSource.getAbsolutePath()}/libvlc/jni/libs/${a.architecture}/"
        into "src/main/jniLibs/${a.architecture}/"
        finalizedBy 'vlcCopyJavaFiles'
        onlyIf { project.ext.vlcAndroidSource.exists() }
    }
}

task vlcCopyJavaFiles(type: Copy) {
    group = 'Vlc'
    description = 'Copy source from VLC Android source code to project if exists'
    from "${project.ext.vlcAndroidSource.getAbsolutePath()}/libvlc/src/"
    into "src/main/java"
    onlyIf { project.ext.vlcAndroidSource.exists() }
}

task vlcBuild() {
    group = 'Vlc'
    description = 'Build Vlc source code and copies resulting libVlc libraries into project'
    dependsOn { tasks.findAll { task -> task.name.startsWith('vlcBuild') && task.name != 'vlcBuild' } }
}
